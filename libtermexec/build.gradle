apply plugin: 'com.android.library'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        //Major.Minor.Patch
        versionCode 10001
        versionName '1.0.1'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// by default recent plugin version does not copy any AIDL files "to avoid publishing too much"
android.libraryVariants.all { variant ->
    Sync packageAidl = project.tasks.create("addPublic${variant.name.capitalize()}Aidl", Sync) { sync ->
        from "$project.projectDir/src/main/aidl/"
        into "$buildDir/intermediates/bundles/${variant.dirName}/aidl/"
    }

    Task javaCompileTask
    if (variant.hasProperty('javaCompileProvider')) {
        // gradle plugin 3.3.+
        javaCompileTask = variant.javaCompileProvider.get()
    } else {
        javaCompileTask = variant.javaCompile
    }
    javaCompileTask.dependsOn packageAidl
}

dependencies {
    implementation project(':emulatorview')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.annotation:annotation:1.1.0"
}
